<?xml version="1.0"?>

<!DOCTYPE html>

<html xmlns="http://www.w3.org/1999/xhtml">
  <head>
    <title>Dan Shoutis</title>
    <link href="http://fonts.googleapis.com/css?family=Cardo:regular" rel="stylesheet" type="text/css"></link>
    <link  href="http://fonts.googleapis.com/css?family=OFL+Sorts+Mill+Goudy+TT:regular,italic" rel="stylesheet" type="text/css" ></link>
    <link href="resume.css" rel="stylesheet" type="text/css"></link>
    <script src="cfa.js" type="text/javascript"></script>
    <script type="text/javascript">
      function go() {
        var c = document.getElementById('eyecandy');
	if (!!c.getContext) {
	  document.getElementById('descr').className = 'viz';
          CFA.exec(CFA.parse('the_ta'),[-70,-30,70,50],'eyecandy');
        } else {
          c.parentNode.removeChild(c);
        }
      }
    </script>
  </head>

  <body onload='go();'>

    <div id="holder">
      <canvas id="eyecandy" width="200" height="200"></canvas>
      <div id="descr">
	<a href="http://dl.dropbox.com/u/6600185/cfa/cfademo.html?utm_source=resume&utm_medium=email&utm_campaign=resume">What's this</a>?
      </div>
    </div>
    <!-- INCLUDE -->

    <div class="ta" style="display:none;">
      <form>
	<textarea id='the_ta' rows='10' cols='80'>
	  /* 
 * Seasons, by michael @ contextfreeart.org:
 * http://www.contextfreeart.org/gallery/view.php?id=519 
 */
background {b -.8 hue 230 sat .7 a-0.85}
 
startshape SEASONS
 
rule SEASONS {
    SKY {y 2 b 1 hue 180 z -100 sat .1}
    TRUNK {x 30 hue 30  alpha -.5 flip 90}
    TRUNK {x -30 hue 90 alpha -.5}
}
 
rule SKY {
    80* {y 1 hue .6 sat .01} SQUARE {s 100 1.3}
}
 
rule TRUNK 50 {
    BARK {}
    TRUNK {y .2 r .1}
}
rule TRUNK {
    2* {flip 90} LIMB {r -12 s .9}
}
 
rule LIMB 50 {
    BARK {}
    LIMB {y .2 r .1}
}
rule LIMB {
    2* {flip 90} BRANCH {r 8 s .9}
}
 
rule BRANCH 10 {
    BARK {}
    BRANCH {y .2 hue -2 r .1}
}
rule BRANCH 10 {
    BARK {}
    BRANCH {y .2 r .1 hue 2}
}
rule BRANCH 10 {
    BARK {}
    BRANCH {y .2 r .1 z 1}
}
rule BRANCH 10 {
    BARK {}
    BRANCH {y .2 r .1 z -1}
}
rule BRANCH {
    2* {flip 90 hue 30} BOUGH {r 10 s .7}
}
 
rule BOUGH {
    LEAVES {}
    TWIG {z -1}	
}
 
rule TWIG 200 {
    BARK {}
    TWIG {y .2 r .1 s .999 a -.001}
}
rule TWIG {
    TWIG {r 12 s .8}
    TWIG {r -12 s .8 f 90}
}
rule TWIG {
    TWIG {r 8 s .9}
    TWIG {r -18 s .7 f 90}
}
rule TWIG {
    TWIG {r 25 s .6}
    TWIG {r -5 s .95 f 90}
}
 
rule LEAVES {
    60* {r .2} LEAF {y 30 r -180 sat 1 b .6 s 4 hue -10}
    100* {r .2} LEAF {y 50 r -180 sat 1 b .8 s 4 hue -10}
    60* {r .2} LEAF {y 70 r -180 sat 1 b 1 s 4 hue 10}
}
 
rule LEAF 3 {
    LEAF {x 1 hue 1 b -.05 z -1 s .99}
}
rule LEAF 4 {
    LEAF {r 138 sat -.03 z 1}
}
rule LEAF {
    SQUARE {skew 20 20 hue -10}
    SQUARE {skew 30 30 sat .1}
    SQUARE {skew 40 40 hue 10 sat .2}
    SQUARE {skew 20 20 b -1 s 1.5 z -.1 alpha -.8}
    SQUARE {skew 20 20 b -1 s 2.2 z -.1 alpha -.9}
}
 
rule BARK {
    CIRCLE {a -.7 s 2}
    CIRCLE {a -.7 s 1.5 b .3 z .1 x .3}
    CIRCLE {a -.7 s 1 b .6 z .2 x .6}
    CIRCLE {a -.7 s .5 b .9 z .3 x .9}
}
rule BARK 3 {
    CIRCLE {a -.6 s 2}
    CIRCLE {a -.6 s 1.5 b .3 z .1 x .3}
}
	</textarea>
      </form>
    </div>
  </body>

</html>
